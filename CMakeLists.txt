CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

#SET(CMAKE_VERBOSE_MAKEFILE on)

# provide only Debug and Release configurations
SET(CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "build config types" FORCE)
IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE Debug CACHE STRING "build type: Debug or Release" FORCE)
ENDIF(NOT CMAKE_BUILD_TYPE)

IF(NOT IS_ABSOLUTE ${CMAKE_INSTALL_PREFIX})
    MESSAGE(FATAL_ERROR "CMAKE_INSTALL_PREFIX has to be an absolute path!")
ENDIF(NOT IS_ABSOLUTE ${CMAKE_INSTALL_PREFIX})


PROJECT(HIPACC)
SET(HIPACC_MAJOR_VERSION 0)
SET(HIPACC_MINOR_VERSION 7)
SET(HIPACC_PATCH_VERSION 0)
SET(HIPACC_VERSION ${HIPACC_MAJOR_VERSION}.${HIPACC_MINOR_VERSION}.${HIPACC_PATCH_VERSION})
SET(HIPACC_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
SET(HIPACC_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR})

OPTION(USE_POLLY "Use Polly for analysis" OFF)
INCLUDE(CMakeDependentOption)
CMAKE_DEPENDENT_OPTION(USE_JIT_ESTIMATE "Compile kernels JIT to estimate resource usage" ON "NOT APPLE" OFF)


# get repository and revision
EXECUTE_PROCESS(COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/cmake/scripts/get_repository_path.sh
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    TIMEOUT 5
    RESULT_VARIABLE git_result
    OUTPUT_VARIABLE HIPACC_GIT_REPOSITORY)
STRING(STRIP ${HIPACC_GIT_REPOSITORY} HIPACC_GIT_REPOSITORY)

EXECUTE_PROCESS(COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/cmake/scripts/get_source_version.sh
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    TIMEOUT 5
    RESULT_VARIABLE git_result
    OUTPUT_VARIABLE HIPACC_GIT_VERSION)
STRING(STRIP ${HIPACC_GIT_VERSION} HIPACC_GIT_VERSION)


# add path for custom modules
SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake/modules)
FIND_PACKAGE(LLVM REQUIRED)
INCLUDE_DIRECTORIES(${LLVM_INCLUDE_DIR})

FIND_PACKAGE(CUDA)
FIND_PACKAGE(OpenCL)
FIND_PACKAGE(RenderScript)
FIND_PACKAGE(OpenCV)

IF(NOT CUDA_FOUND AND NOT OPENCL_FOUND AND NOT RENDERSCRIPT_FOUND)
    MESSAGE(FATAL_ERROR "Neither CUDA, OpenCL nor RenderScript found. At least one of them is required to use HIPACC properly.")
ENDIF(NOT CUDA_FOUND AND NOT OPENCL_FOUND AND NOT RENDERSCRIPT_FOUND)

IF(CUDA_FOUND)
    SET(NVCC_COMPILER "${CUDA_TOOLKIT_ROOT_DIR}/bin/nvcc")
    IF(CUDA_VERSION VERSION_LESS "6.0")
        SET(USE_JIT_ESTIMATE OFF)
    ELSE()
        INCLUDE_DIRECTORIES(${CUDA_TOOLKIT_INCLUDE})
    ENDIF()
ELSE(CUDA_FOUND)
    SET(USE_JIT_ESTIMATE OFF)
ENDIF(CUDA_FOUND)

IF(OPENCL_FOUND)
    SET(CL_COMPILER "${CMAKE_INSTALL_PREFIX}/bin/cl_compile")
    MARK_AS_ADVANCED(CUDA_BUILD_CUBIN CUDA_BUILD_EMULATION CUDA_HOST_COMPILER
        CUDA_SDK_ROOT_DIR CUDA_TOOLKIT_ROOT_DIR CUDA_VERBOSE_BUILD)
ENDIF(OPENCL_FOUND)

MESSAGE(STATUS "Configuration summary:")
MESSAGE(STATUS "===")
MESSAGE(STATUS "USE_POLLY=${USE_POLLY}")
MESSAGE(STATUS "USE_JIT_ESTIMATE=${USE_JIT_ESTIMATE}")
MESSAGE(STATUS "===")


# platform specific fixes
IF(APPLE)
    SET(CUDA_LINK "-Xlinker -framework,CUDA")
    SET(CUDA_COMP "-ccbin ${CLANG_EXECUTABLE} -Xcompiler -arch -Xcompiler x86_64 -Xcompiler -stdlib=libstdc++")
    SET(TIME_LINK "")
ELSE(APPLE)
    SET(CUDA_LINK "-lcuda -lcudart")
    SET(CUDA_COMP "")
    SET(TIME_LINK "-lrt -lm -lstdc++")
ENDIF(APPLE)


# debug and release builds
STRING(TOUPPER "${CMAKE_BUILD_TYPE}" build)
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
IF(build STREQUAL "RELEASE")
    SET(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE}")
ELSE()
    SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -Wall -Wunused")
ENDIF()


# set include directory, add src directories
INCLUDE_DIRECTORIES(
    ${CMAKE_CURRENT_SOURCE_DIR}/include
    ${CMAKE_CURRENT_BINARY_DIR}/include)
ADD_SUBDIRECTORY(lib)
ADD_SUBDIRECTORY(compiler)
ADD_SUBDIRECTORY(tools)



# configure header files to pass some of the CMake settings to the source code
CONFIGURE_FILE(
    "${HIPACC_SOURCE_DIR}/include/hipacc/Config/config.h.cmake"
    "${HIPACC_BINARY_DIR}/include/hipacc/Config/config.h")
CONFIGURE_FILE(
    "${HIPACC_SOURCE_DIR}/tests/Makefile.cmake"
    "${HIPACC_BINARY_DIR}/tests/Makefile")
CONFIGURE_FILE(
    "${HIPACC_SOURCE_DIR}/tests/Makefile_test.cmake"
    "${HIPACC_BINARY_DIR}/tests/Makefile_test")
CONFIGURE_FILE(
    "${HIPACC_SOURCE_DIR}/tests/Makefile_test_opencv.cmake"
    "${HIPACC_BINARY_DIR}/tests/Makefile_test_opencv")
CONFIGURE_FILE(
    "${HIPACC_SOURCE_DIR}/tests/CMakeLists.txt.cmake"
    "${HIPACC_BINARY_DIR}/tests/CMakeLists.txt"
    @ONLY)


# install dsl and runtime header files
FILE(GLOB dsl_headers "${CMAKE_CURRENT_SOURCE_DIR}/dsl/*.hpp")
FILE(GLOB runtime_headers "${CMAKE_CURRENT_SOURCE_DIR}/runtime/*.hpp")
INSTALL(FILES ${runtime_headers} DESTINATION include)
INSTALL(FILES ${dsl_headers} DESTINATION include/dsl)
# install tests
INSTALL(DIRECTORY tests
    DESTINATION ${CMAKE_INSTALL_PREFIX}
    PATTERN "*.cmake" EXCLUDE)
INSTALL(DIRECTORY ${HIPACC_BINARY_DIR}/tests/
    DESTINATION ${CMAKE_INSTALL_PREFIX}
    PATTERN "*Makefile_test*" EXCLUDE)

# install Makefiles for test cases
FILE(GLOB TEST_DIRS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/tests ${CMAKE_CURRENT_SOURCE_DIR}/tests/*)
FOREACH(DIR IN LISTS TEST_DIRS)
    IF(IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/tests/${DIR})
        IF(${DIR} MATCHES "opencv_*")
            INSTALL(FILES ${HIPACC_BINARY_DIR}/tests/Makefile_test_opencv
                DESTINATION tests/${DIR}
                RENAME Makefile)
        ELSE(${DIR} MATCHES "opencv_*")
            INSTALL(FILES ${HIPACC_BINARY_DIR}/tests/Makefile_test
                DESTINATION tests/${DIR}
                RENAME Makefile)
        ENDIF(${DIR} MATCHES "opencv_*")
    ENDIF(IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/tests/${DIR})
ENDFOREACH(DIR IN LISTS OPENCV_TEST_DIRS)

